
; ============================================================================
; render_frame — top-level entry. Walks the BSP from the root,
; visiting subsectors in front-to-back order, dispatching to the
; per-subsector handler (render_subsector).
;
; Caller must have:
;   - Loaded WAD ROM into memory.
;   - (ROM bases are layout.inc constants since 2026-07-10 — no pointer setup.)
;   - Set up player view state (zp_br_px, etc.) and called view_setup.
;   - Initialized the span pool (via span_init at $2000).
;   - Cleared the framebuffer.
;
; Algorithm — iterative front-to-back walk with an explicit stack.
; Recursive Python reference (packed_render_bsp, doom_wireframe.py):
;   def render(nid):
;     if clips.is_full(): return
;     if nid & 0x8000: render_subsector(nid & 0x7fff); return
;     side = point_on_side(px, py, node)               # br_node_setup
;     render(children[side])                           # near child, NO TEST
;     if clips.is_full(): return
;     if bbox_visible(node, side ^ 1):                 # far child, LATER
;         render(children[side ^ 1])
;
; THE NEAR CHILD IS NEVER TESTED (2026-09-04, Eben) — this is DOOM's own
; shape: r_bsp.c descends bsp->children[side] unconditionally and wraps
; only children[side^1] in R_CheckBBox.  The near child is the half-space
; the viewer stands in, so the test rejected just 5% of the time (1.9
; subtrees/frame) while being charged on every node walked (~37.7
; calls/frame, ~460 cyc each).  Measured -2.8% over the 3,960-pose armour
; grid, -3.9% on its hottest tenth, worst frame -6%; cheap frames lose
; ~0.3%.  Over-descent draws NOTHING extra — the segs of an invisible
; subtree are rejected individually — so no pixel moves; both Python
; models dropped the same test in the same commit, so the traversal
; differential still compares like with like.
; The 6502 flattens the recursion: visiting a node descends the NEAR child
; straight away, but first pushes a DEFERRED (node, farside) entry.  The
; far child's bbox + has_gap check runs when that deferred entry is POPPED
; — i.e. after the entire near subtree has rendered — so it queries exactly
; the span/occlusion state the Python recursion sees at its bbox_visible
; call.
;
; The traversal is RECURSIVE on the hardware stack since 2026-07-14:
; rc_node renders one internal node; it pushes (node id, far side) as
; locals, recurses on the near child, and tail-calls on the far.
; zp_bsp_stack_sp EVICTED FROM ZERO PAGE 2026-08-22: two accesses per
; frame (tools/zpheat.py), a plain scalar, and zero page is the binding
; constraint on bringing the TFS sweep state in.
zp_bsp_stack_sp = $0C28
; zp_bsp_stack_sp = the saved S for the is_full unwind. Ids are u8
; end to end (2026-07-15) — a child's subsector-ness lives in its
; PARENT's TYPE byte (NF_RLEAF/NF_LLEAF), not in the link.
; (br_init_frame retired 2026-07-15: the per-frame init lives inline at
; render_frame entry below; the jt slot and its harness/driver
; callers are gone — partial-flow harnesses poke the state from Python,
; see bsp_render_6502.poke_init_frame_state.)

render_frame:
.scope bsp_walk                         ; (named scope: historical — the
                                        ; bv_site SMC patching is retired)
; L0 anchor: the traversal's bank invariant (node_setup and the
; subsector serve no longer page — every path keeps L0 until the
; clipper/angle modules page for themselves and the child follows
; restore it). One PAGE per frame covers the seed.
   PAGE BANK_WALK
.if ::MASTER
.import mf_frame
   JSR mf_frame                         ; MASTER: a new frame for the plane
.endif                                  ; caches (master/mfill.s)

; --- Per-frame init (the standalone br_init_frame is retired).
; Records-pointer ground state: the lo byte is never written non-zero
; anywhere (record pages are page-aligned) and every draw site
; arms/disarms the hi byte explicitly. Then the vxcache valid-bitmap
; clear — entries are player-relative, so every frame starts cold
; (the VWH projection cache, by contrast, is self-validating and
; persists). 60-byte clear (59 used + 1 pad, inside the vxcache
; reservation up to $1B3F), four 15-byte stripes off one X. ---
   LDA #0                               ; A = 0 RIDES into the wipes below
                                        ; — NOT a C02/STZ candidate
                                        ; (the zp_dcl_rec_buf pair's clear
                                        ; died 2026-08-25: last reader gone
                                        ; with the records machinery)
; VXCACHE_VALID + VDONE wipe (re-striped 2026-08-13: 19 stores x 3
; iterations x 2 bitmaps = 114 B — the 57-byte EXACT extent for 455
; ids, and ~40 cyc cheaper than the old 24x5/120-byte shape).
   LDX #2
bif_clr2:
   STA VXCACHE_VALID_BASE,X
   STA VXCACHE_VALID_BASE+3,X
   STA VXCACHE_VALID_BASE+6,X
   STA VXCACHE_VALID_BASE+9,X
   STA VXCACHE_VALID_BASE+12,X
   STA VXCACHE_VALID_BASE+15,X
   STA VXCACHE_VALID_BASE+18,X
   STA VXCACHE_VALID_BASE+21,X
   STA VXCACHE_VALID_BASE+24,X
   STA VXCACHE_VALID_BASE+27,X
   STA VXCACHE_VALID_BASE+30,X
   STA VXCACHE_VALID_BASE+33,X
   STA VXCACHE_VALID_BASE+36,X
   STA VXCACHE_VALID_BASE+39,X
   STA VXCACHE_VALID_BASE+42,X
   STA VXCACHE_VALID_BASE+45,X
   STA VXCACHE_VALID_BASE+48,X
   STA VXCACHE_VALID_BASE+51,X
   STA VXCACHE_VALID_BASE+54,X
   STA VDONE,X
   STA VDONE+3,X
   STA VDONE+6,X
   STA VDONE+9,X
   STA VDONE+12,X
   STA VDONE+15,X
   STA VDONE+18,X
   STA VDONE+21,X
   STA VDONE+24,X
   STA VDONE+27,X
   STA VDONE+30,X
   STA VDONE+33,X
   STA VDONE+36,X
   STA VDONE+39,X
   STA VDONE+42,X
   STA VDONE+45,X
   STA VDONE+48,X
   STA VDONE+51,X
   STA VDONE+54,X
   DEX
   BPL bif_clr2

; --- RECURSIVE BSP traversal on the hardware stack ------------------
; This is a direct 6502 mirror of the reference pseudocode:
;
;   def render_frame():            #  (seed)
;       rc_node(ROOT)              #  root is internal by construction
;
;   def rc_node(id):               #  id arrives in zp_node_ch_l
;       if is_full(): unwind()     #  screen solid: nothing can show
;       side = node_setup(id)      #  player side of the partition
;       push id, side^1            #  the continuation's locals
;       descend(child[id][side])              # near, as a CALL — NO TEST
;       side, id = pop, pop
;       if is_full(): unwind()
;       if bbox_visible(id, side):
;           descend(child[id][side])          # far, as a TAIL CALL
;
;   def descend(c):                #  leaf bit lives in the PARENT's
;       if leaf: render_subsector(c)          # TYPE byte (u8 ids,
;       else:    rc_node(c)                   # no link hi bytes)
;
; unwind() = one TXS to the S saved at frame entry: every pending
; frame vanishes at once and the RTS returns to our caller. JSR depth
; accrues only down near chains (depth x 4 stack bytes; the game loop
; runs interrupt-free). is_full sits exactly where the old iterative
; loop checked it, so the serve/bbox query sequence is walkseq-
; identical; it reads zp_head directly (unbanked — no paging).

; (FETCH_CHILD retired 2026-07-15: the side-specialised rc_node bodies
; inline each arm directly — see rc_node below.)

; --- seed: rc_node(ROOT) ---
   TSX
   STX zp_bsp_stack_sp                     ; unwind target
   LDA #<LAY_ROOT                          ; layout.inc constant (u8)
   STA zp_node_ch_l
   JMP rc_node_nc                          ; frame start is provably not
                                        ; full — skip the is_full entry


; ============================================================================
; NODE_SETUP_DISPATCH s0, s1 — point-on-side, inlined (moved from lo.s
; 2026-07-16; the walk is the single caller). Tests the player against
; node zp_node_ch_l's partition and JMPs s0 (right of line) / s1
; (left/on) DIRECTLY — no A verdict, no RTS round trip.
;   Inputs:  zp_node_ch_l = node id (u8); zp_br_pxraw/pyraw = player
;            position, RAW map units (s16 — the side test must not lose
;            a weak axis to /8 truncation). Bank L0 paged (callers all
;            hold it).
;   Axis nodes (TYPE forms 0-3, 73%): ONE strict s16 compare against
;   the NX/NY origin plane, direction sign baked at pack time.
;   General (TYPE 4): DIR delta form sharing CROSS_MAG_DECIDE with the
;   back-face test. Ties -> s1 everywhere (the mirror's D==0 rule).
;   Python mirror: doom_wireframe.point_on_side (raw s16 values).
;   Clobbers A, X; the shared cross slots + t0-t5 on the general path.
; ============================================================================
.macro NODE_SETUP_DISPATCH s0, s1
.local ns_t_general, ns_py_gt, ns_x0, ns_x1
   LDX zp_node_ch_l
   LDA NODE_TYPE,X
   AND #NT_MASK                            ; bits 7/6 are the child leaf flags
; --- sense-normalized dispatch (2026-07-16): the packer child-swaps
; every '<' axis node into the '>' sense at load, so only TWO axis
; forms exist and the dispatch is 3-way: 0 px> (fall), 1 py>, 2 general.
   LSR A                                   ; 0:(A0 C0) 1:(A0 C1) 2:(A1 C0)
   BNE ns_t_general
   BCS ns_py_gt
; form 0: side0 iff px > nx — REVERSED subtract (2026-07-16): testing
; nx - px puts the tie on the fall-through side for free (side0 iff
; the diff is strictly negative). Same rule for form 1 (py). Ties fall
; to side1 (D == 0 -> side 1, the mirror's rule, post-normalization).
   LDA NODE_NXLO,X                         ; the plane bakes 2*nx; px2 is
   CMP zp_br_px2_l                         ; the tie-broken doubled raw —
   LDA NODE_NXHI,X                         ; 'px_true > nx' EXACTLY, ties
   SBC zp_br_px2_h                         ; included, at the original
   BMI ns_x0                               ; two-byte compare (2026-08-26)
   JMP s1                                  ; tie or less -> side1
ns_x0:
   JMP s0
ns_py_gt:
; form 1: side0 iff py > ny — reversed like form 0
   LDA NODE_NYLO,X
   CMP zp_br_py2_l
   LDA NODE_NYHI,X
   SBC zp_br_py2_h
   BMI ns_x0                               ; ny < py -> side0
   JMP s1                                  ; tie or less -> side1
ns_t_general:
; --- general partition: DIR delta form (2026-07-15) — the packer bakes
; the gcd-reduced primitive direction as (NODE_DIRID, NODE_DSGN —
; b7 ndy neg / b6 ndx neg), sharing the seg DIR
; tables. Deltas against the origin planes stage into the SHARED cross
; slots, the sign shortcut mirrors bf_g_both, and the magnitude tier is
; the SAME CROSS_MAG_DECIDE core the back-face test expands: side0 is
; "front" (D = ndy*dx - ndx*dy > 0), ties side1. The old raw s16 x s16
; double-smul cascade (and br_smul_s16_s16_s32) is gone.
; the whole general body is ONE shared subroutine (2026-08-26 — funded
; the exact-descent band): DSGN/DIRID staging, deltas, sign shortcut,
; products, banded verdict. C comes back as the side.
   JSR node_side_general
   BCS ns_x0                               ; C=1 -> side0
   JMP s1
.endmacro

; ============================================================================
; node_side_general — the general (diagonal) point-on-side body, ONE
; copy for both NODE_SETUP_DISPATCH expansions (walk + pm_find_ss).
;   in : X = node id (zp_node_ch_l), zp_br_sign = NODE_DSGN,
;        zp_bf_dir = DIR id, C = 1 (the dispatch LSR of NT_GEN);
;        zp_br_pxraw/pyraw + PM_FXW/+2 = the position.
;   out: C=1 -> side0 (D_true > 0), C=0 -> side1. EXACT everywhere
;        (2026-08-26): out-of-band by sign, in-band via node_band.
;   Clobbers A, X, deltas, t0-t5, mul workspace.
; ============================================================================
.scope
::node_side_general:
   LDA NODE_DSGN,X
   STA zp_br_sign                          ; b7 = sgn ndy, b6 = sgn ndx
   LDA NODE_DIRID,X
   STA zp_bf_dir                           ; DIR-table index
; dx = pxraw - nx (s16); hi rides A for the zero test. NO SEC: the
; dispatch LSR of NT_GEN=3 left C=1, and JSR + the loads preserved it.
   LDA zp_br_pxraw_l
   SBC NODE_NXLO,X
   STA zp_br_dx_l
   LDA zp_br_pxraw_h
   SBC NODE_NXHI,X
   STA zp_br_dx_h
   ORA zp_br_dx_l
   BEQ nsg_dx0
; dy = pyraw - ny (s16)
   LDA zp_br_pyraw_l
   SEC
   SBC NODE_NYLO,X
   STA zp_br_dy_l
   LDA zp_br_pyraw_h
   SBC NODE_NYHI,X
   STA zp_br_dy_h
   ORA zp_br_dy_l
   BEQ nsg_dy0                             ; dy==0: D = P1
; sign shortcut (mirror of bf_g_both): opposite product signs decide
; with no multiply; sign(D) = sign(P1). EXACT: both deltas nonzero
; here, so |D| = |P1|+|P2| >= ndy+ndx = sum > the fraction error.
; Diagonal sense is NORMALIZED (2026-08-20): ndy > 0 always, so
; sign(P1) = sign(dx) — the EOR died.
   LDX zp_br_dx_h                          ; b7 = sign(P1), rides X
   LDA zp_br_sign
   ASL A                                   ; b6 (ndx sign) -> b7
   EOR zp_br_dy_h                          ; b7 = sign(P2)
   STA zp_br_t2
   TXA
   EOR zp_br_t2                            ; b7 set = opposite signs
   BPL nsg_mul                             ; same sign -> magnitude core
   TXA                                     ; opposite: sign(D) = sign(P1)
   BMI nsg_s1
   SEC                                     ; positive: side0
   RTS
nsg_s1:
   CLC
   RTS
; dx == 0: |P1| = 0 — the int verdict is IN BAND whenever ndx*|dy| is:
; no shortcut arm; stage sign(P2) and run the banded product core
; (P1's mul is a cheap 0*n). Same for dy == 0, and for dx == dy == 0
; the core ties into the fraction refine (D_int = 0).
nsg_dx0:
; (no SEC: entered iff dx == 0 — a zero subtract result means no
;  borrow, C is already 1)
   LDA zp_br_pyraw_l
   SBC NODE_NYLO,X
   STA zp_br_dy_l
   LDA zp_br_pyraw_h
   SBC NODE_NYHI,X
   STA zp_br_dy_h
   LDA zp_br_sign
   ASL A                                   ; b7 = sgn ndx
   EOR zp_br_dy_h                          ; b7 = sign(P2) (P1 = 0: the
   TAX                                     ; core's 'shared sign' is P2's)
   JMP nsg_mul
; dy == 0: D = P1 = ndy*dx; sign(P1) = sign(dx) (ndy > 0 normalized)
nsg_dy0:
   LDX zp_br_dx_h
nsg_mul:
; The DIR planes are BANK A ONLY (2026-08-30), and so is ROM_DBOUND_C --
; which is the real story here.  cross_side_banded's band gate reads
; ROM_DBOUND_C from whatever bank is live, and this path ran under WALK,
; where $B880 holds 43 stray bytes of DIR-B stride shadow instead of the
; table.  Comparing against ~zero means BCS csb_front is ALWAYS taken, so
; THE EXACT-DESCENT BAND REFINE WAS DEAD on the node path in the banked
; build.  Flat never saw it: one copy, no banks.  Paging SEG for the whole
; chain fixes that and lets the bank-B DIR duplicate go; only node_band's
; NODE_DSGN read wants WALK, and it pages for itself.
   PAGE BANK_SEG
   JSR cross_products_banded
   PAGE BANK_WALK
   RTS
.endscope

; ============================================================================
; cross_side_banded — node point-on-side verdict from the staged cross
; products, EXACT (2026-08-26, the backface finding applied to the
; traversal). Out-of-band the truncated compare's sign IS the true sign;
; in-band (|D| < ROM_DBOUND_C[dir] — conservative: the node error is
; only the dropped position fraction, < sum, K-free since node origins
; are exact map vertices) node_band refines T = 256*D + ndy*fxw - ndx*fyw.
;   in : t2/t3/t4 = |P1| u24, t0/t1/t5 = |P2| u24, zp_br_sign b7 =
;        shared product sign, zp_bf_dir, zp_node_ch_l live,
;        PM_FXW/+2 = world fraction bytes (pmf_cand stages them).
;   out: C=1 -> side0 (D_true > 0), C=0 -> side1 (ties side1 — the
;        float mirror's rule). Clobbers A, X, t0-t4, mul workspace.
; ============================================================================
.scope
::cross_side_banded:
; ONE full s24 subtract, then classify (2026-08-26 size cut: the
; d16 mid-tier + separate senior-diff path merged — this path already
; paid two umul8s, the flat subtract is noise):
   SEC
   LDA zp_br_t2
   SBC zp_br_t0
   STA zp_br_t0                            ; d lo
   LDA zp_br_t3
   SBC zp_br_t1
   STA zp_br_t1                            ; d mid
   LDA zp_br_t4
   SBC zp_br_t5                            ; A = d hi
   BCC csb_neg
   ORA zp_br_t1
   BNE csb_front                           ; d >= 256: far
   LDA zp_br_t0
   BMI csb_front                           ; d in [128,255]: far (bound<=95)
csb_bp:
   LDX zp_bf_dir                           ; band gate, |d| in A (== t0 on
   CMP ROM_DBOUND_C,X                      ; the positive path)
   BCS csb_front                           ; |d| >= bound: exact by sign
   STA zp_br_t0
   LDA zp_br_sign                          ; sign(D) = shared sign
   STA zp_br_t1
   JMP node_band                           ; tail: returns C to the caller
csb_neg:
   AND zp_br_t1
   CMP #$FF
   BNE csb_back                            ; d <= -257: far
   LDA zp_br_t0
   BEQ csb_back                            ; d = -256: far
   BPL csb_back                            ; d in [-256,-129]: far
   EOR #$FF
   CLC
   ADC #1                                  ; |d| in [1,128]
   STA zp_br_t0
   LDA zp_br_sign                          ; negative d: FLIP the shared
   EOR #$80                                ; sign in place — csb_bp's gate,
   STA zp_br_sign                          ; stage and far-exit then read
   LDA zp_br_t0                            ; sign(D) directly
   JMP csb_bp
csb_front:                                 ; |P1| > |P2| out of band:
   LDA zp_br_sign                          ; D = +sign -> side0 iff sign+
   BMI csb_s1
csb_s0:
   SEC
   RTS
csb_back:                                  ; |P1| < |P2| out of band:
   LDA zp_br_sign                          ; D = -sign -> side0 iff sign-
   BMI csb_s0
csb_s1:
   CLC
   RTS
.endscope

; ============================================================================
; node_band — the exact in-band node verdict.
;   T (s24) = 256*D + ndy*fxw - ndx*fyw  — the true dot x256 against the
;   full-fraction position (node lines pass through exact map vertices,
;   so there is NO K-residue term; ndy > 0 by sense normalization).
;   in : zp_br_t0 = |D| u8, zp_br_t1 b7 = sign(D), zp_bf_dir,
;        zp_node_ch_l (NODE_DSGN b6 = sgn ndx), PM_FXW/+2 world fracs.
;   out: C=1 -> T > 0 (side0); C=0 -> T <= 0 (side1; exact ties side1).
; ============================================================================
.scope
::node_band:
   LDA #0
   STA zp_br_t2                            ; T = D << 8 (signed)
   STA zp_br_t5                            ; first bb_apply = ADD
   TAX
   LDA zp_br_t0
   BEQ nb_ipos                             ; D == 0: ext MUST be 0 (either
   BIT zp_br_t1                            ; staged sign arrives)
   BPL nb_ipos
   EOR #$FF
   CLC
   ADC #1
   LDX #$FF
nb_ipos:
   STA zp_br_t3
   STX zp_br_t4
; + ndy * fxw (ndy > 0 normalized; t5 staged 0 at entry)
   LDX zp_bf_dir
   LDA ROM_DIRS_C + LAY_MAX_DIRS,X         ; ndy
   LDX PM_FXW
   STX zp_mul_b
   JSR umul8
   JSR bb_apply
; -+ ndx * fyw: subtract iff ndx > 0 (DSGN b6 = ndx NEGATIVE)
   PAGE BANK_WALK                          ; the ONE bank-B read in the chain
   LDX z:zp_node_ch_l
   LDA NODE_DSGN,X
   AND #$40                                ; $40 = ndx neg -> ADD (t5 b7=0)
   EOR #$40
   ASL A                                   ; ndx pos -> $80 = subtract
   STA zp_br_t5
   PAGE BANK_SEG                           ; back for |ndx|.  node_band must
   LDX zp_bf_dir                           ; EXIT under SEG: it is the chain
   LDA ROM_DIRS_C,X                        ; tail for the backface caller too
   LDX PM_FXW+2
   STX zp_mul_b
   JSR umul8
   JSR bb_apply
nb_v:
   LDA zp_br_t4
   BMI nb_le                               ; T < 0 -> side1
   ORA zp_br_t3
   ORA zp_br_t2
   BEQ nb_le                               ; T == 0 -> side1 (float rule)
   SEC
   RTS
nb_le:
   CLC
   RTS
.endscope

; descend: id in zp_node_ch_l, N = the child's leaf bit (staged by the
; caller's TYPE load, +ASL for left arms — flags ride through JSR/JMP).
; ONE leaf test per class (2026-07-16, was 4 site copies).
;
; ONE ENTRY (2026-09-05): rc_descend_near is GONE and both near-child
; sites JSR here.  It used to test is_full on the way in, and that test
; was DEAD -- provably, not just empirically (it fired 0.00 times a frame
; across the suite against 23.4 tests).  The near child is reached only
; through rc_node_nc, rc_node_nc is entered only from the root seed (not
; full by construction), from this entry's own fall-through (the far
; site tested just before its bbox check) or from a near descent that
; had itself just tested -- and between rc_node_nc and the near JSR the
; only code that runs is point_on_side, the DSGN gate and a child fetch.
; NOTHING DRAWS THERE, so zp_head cannot have changed since the last
; test.  The old note claimed near and far "differ in their is_full
; contracts"; they never did.
;
; This is r_bsp.c's own shape: R_RenderBSPNode carries no occlusion test
; on the way down at all and prunes only at R_CheckBBox, which is where
; our two surviving tests sit (r0_far / r1_far, 3-5% fire rate).
rdf_leaf:
   JMP render_subsector

rc_descend_far:
   BMI rdf_leaf                            ; leaf: straight to render
; SIDE-SPECIALISED (2026-07-15): node_setup returns side in A with Z
; live (every exit is LDA #imm / RTS), so ONE dispatch selects a
; right-then-left or left-then-right body with the child fetches
; INLINED per arm — no runtime side test in the fetches, no side on
; the stack (the continuation's side is its code position; only the id
; is pushed), and the bbox side stores reuse A / a bare immediate.
rc_node_nc:                             ; far-node fall-in (is_full done)
   NODE_SETUP_DISPATCH rc_s0, rc_n1        ; point-on-side: JMPs straight
                                        ; to the side body (no verdict
                                        ; register, no return trip)
rc_s0:
; === side 0: near = RIGHT child, far = LEFT child ===
   LDA zp_node_ch_l
   PHA                                     ; push id (side is implicit)
; SAME-AS-PARENT serve (2026-07-17; has_gap re-query retired 2026-07-26):
; DSGN b0 = this node's RIGHT box is byte-identical to the parent box we
; just descended through — identical box => identical angle verdict. The
; has_gap half is inherited too: the descent itself proves the parent's
; fused exit returned 1 for this exact interval, and nothing between that
; verdict and this check touches the spans (descent is node fetches,
; is_full and point-on-side — no leaf renders, so no mark_solid/tighten).
; Same interval + same spans => the re-query is deterministically 1:
; descend as visible, no call. NEAR side only: a far test runs after the
; near subtree drew, so its verdict must be fresh.
   TAX                                     ; A = id (L0 anchored: SoA readable)
; NEAR CHILD: DESCEND, NO TEST (2026-09-04 — see the header).  The DSGN b0
; same-as-parent serve went with it; bank WALK is already held (the
; NODE_SETUP_DISPATCH above read the NODE_* planes), so the fetches below
; are safe without bbox_visible's entry PAGE.
r0_vis:
; (bank WALK held: bbox_visible pages it at entry and its exits never
; re-page — the four child-fetch PAGEs died in the two-bank re-cut)
   LDX zp_node_ch_l
   LDA NODE_CRLO,X                         ; inline RIGHT fetch
   STA zp_node_ch_l
   LDA NODE_TYPE,X                         ; N = NF_RLEAF
   JSR rc_descend_far
r0_far:
   PLA
   TAX                                     ; id for the ADESC gate
   STA zp_node_ch_l                        ; id
   LDA #1
   STA zp_bbox_side                        ; far = LEFT
   SPAN_IS_NOT_FULL
   BEQ bsp_done_full
   PAGE BANK_WALK                          ; DSGN read (bbox_visible would
                                        ; have paged this anyway — its
                                        ; no-page entry is used below)
; DYNAMIC ALWAYS-DESCEND (2026-09-04, Eben).  DSGN b3 (LEFT) / b2 (RIGHT)
; say "last time we descended here, pixels came out": set = skip the
; 571-cycle classify and descend on faith, and the judge below then sets
; or clears the bit from the emitted-segment counter, so a subtree that
; stops drawing costs one wasted descent and goes back to being checked.
; The bit can only ADD descents, never remove one, and over-descent is
; provably harmless.  These are the STATIC always-descend policy's old
; bits: it measured a net loss once this runs, so the bake is off and the
; gate is one plane read for both.
   LDA NODE_DSGN,X
   AND #$08
   BNE r0_far_go                           ; speculate: no test at all
   JSR bbox_visible_l2
   BCC rc_ret                              ; far invisible: this node is done
r0_far_go:
   LDA adyn_ctr                            ; the judge needs a before-sample
   PHA
   LDA zp_node_ch_l                        ; ... and the parent id: the
   PHA                                     ; descent overwrites both
   LDX zp_node_ch_l
   LDA NODE_CLLO,X                         ; inline LEFT fetch
   STA zp_node_ch_l
   LDA NODE_TYPE,X
   ASL A                                   ; N = NF_LLEAF
   JSR rc_descend_far                      ; a CALL now: the judge follows
   JMP adyn_judge_l
; (r0_far_i, the near-invisible arc, died with the near test.)
rc_ret:
   RTS

bsp_done_full:
; unwind(): restore the frame-entry S — every pending frame is gone
; and the caller's return address is back on top. (Parked mid-block,
; between the two side variants: keeps every IS_FULL_B in range and
; the seed fall-in.)
   LDX zp_bsp_stack_sp
   TXS
   RTS

rc_n1:
; === side 1: near = LEFT child, far = RIGHT child === (mirror)
   LDA zp_node_ch_l
   PHA
   TAX                                     ; SAME-AS-PARENT serve, mirror:
; NEAR CHILD: DESCEND, NO TEST (mirror of side 0).
r1_vis:
   LDX zp_node_ch_l
   LDA NODE_CLLO,X                         ; inline LEFT fetch
   STA zp_node_ch_l
   LDA NODE_TYPE,X
   ASL A                                   ; N = NF_LLEAF
   JSR rc_descend_far
r1_far:
   PLA
   TAX                                     ; id for the ADESC gate
   STA zp_node_ch_l
   ZERO zp_bbox_side                      ; far = RIGHT
   SPAN_IS_NOT_FULL
   BEQ bsp_done_full
   PAGE BANK_WALK                          ; (see r0_far)
   LDA NODE_DSGN,X                         ; DSGN b2 = RIGHT (see side 0)
   AND #$04
   BNE r1_far_go
   JSR bbox_visible_l2
   BCC rc_ret1
r1_far_go:
   LDA adyn_ctr
   PHA
   LDA zp_node_ch_l
   PHA
   LDX zp_node_ch_l
   LDA NODE_CRLO,X                         ; inline RIGHT fetch
   STA zp_node_ch_l
   LDA NODE_TYPE,X                         ; N = NF_RLEAF
   JSR rc_descend_far
   JMP adyn_judge_r
; (r1_far_i died with the near test — mirror.)
rc_ret1:
   RTS

; adyn_judge_l / _r — the stack holds (parent id, counter before).
; Pixels since the sample => arm this side's bit, none => disarm it.
; One entry per side so the mask is an immediate.  A/X are dead on a
; walk return (every caller reloads them), Y is untouched.
adyn_judge_l:
   PLA
   TAX                                     ; parent node id
   PLA
   CMP adyn_ctr                            ; unchanged => nothing was drawn
   BEQ ajl_clear
   PAGE BANK_WALK                          ; the subtree paged for itself
   LDA NODE_DSGN,X
   ORA #$08
   STA NODE_DSGN,X
   RTS
ajl_clear:
   PAGE BANK_WALK
   LDA NODE_DSGN,X
   AND #<~$08
   STA NODE_DSGN,X
   RTS
adyn_judge_r:
   PLA
   TAX
   PLA
   CMP adyn_ctr
   BEQ ajr_clear
   PAGE BANK_WALK                          ; the subtree paged for itself
   LDA NODE_DSGN,X
   ORA #$04
   STA NODE_DSGN,X
   RTS
ajr_clear:
   PAGE BANK_WALK
   LDA NODE_DSGN,X
   AND #<~$04
   STA NODE_DSGN,X
   RTS
.endscope

; (bsp_resolve_child was inlined above, 2026-07-14.)

; (br_node_setup lives in lo.s — LO segment, one CODE region both builds)

; (BSP_NEAR/FAR child staging retired 2026-07-15: the walk follows
; children straight from the SoA pages; $096B-$096E are free.)

; ============================================================================
; render_subsector — called per subsector during walk.
;   Input: zp_node_ch_l = subsector id (u8).
;
; (Historical note: this banner predates the real implementation — the
; routine now lives in src/bsp/subsector.s and does exactly the steps
; below, plus deferred solid/tighten routing via the DEFQ op queue.)
;
; Real impl needs to:
;   1. Read subsector header from ROM_SS + id*4: (count, pad, first_seg).
;   2. For each seg in [first_seg, first_seg + count):
;      a. Read seg header from ROM_SEG_HDR + i*12.
;      b. Back-face test (skip if behind).
;      c. Transform vertices (use vxcache).
;      d. Project to screen.
;      e. Emit lines based on seg flags (solid/portal/step/aperture).
;      f. Tighten span list (or mark_solid for solids).
;
; This stub just RTS's; the BSP walker still works and visits all
; subsectors. Useful for verifying traversal in isolation.
; ============================================================================
; --- Per-seg working state ---
; Per-vertex helper outputs (set by br_seg_xform_vertex)
; Back-sector heights (s8 each) — only meaningful for portal segs.
zp_seg_btop_dlt = $0C7A                 ; bch - vz
zp_seg_bbot_dlt = $0C7B                 ; bfh - vz
; Output of bv_proj_one's back-step projection (transient).
zp_seg_sy_btop_lo = $0C7C
zp_seg_sy_btop_hi = $0C7D
zp_seg_sy_bbot_lo = $0C7E
zp_seg_sy_bbot_hi = $0C7F
; Per-seg saved vertex projections live in RAM (ZP $70+ is rasteriser
; territory: RASTER_ZP_SCRSTRT=$70, RASTER_ZP_X0..Y1=$82-$85). Use the
; gap left of the B-region code at $0AA0 ($0A00-$0A9F all free now).
; (SEG_PROJ_BUF retired 2026-07-10: the per-endpoint sy pairs live in the
; packed ZP vertex structs (zp.inc VX1/VX2) — written by do_project_y via
; zp_seg_ep, read by emit through the same zp_seg_sy* names, now 1 cycle
; cheaper as ZP. $0A40-$0A7F now belongs to BBOX_CORNERS alone.)
; Per-vertex view-space integer values, for near-plane crossing math.
; Always populated by br_seg_xform_vertex into "current" slots; the seg
; loop copies into v1/v2 slots so we have both vertices' values when
; computing the crossing point.
; Hot per-vertex view coords promoted to real ZP (were $0A50.. absolute) —
; safe-free ZP (0-access incl. rasteriser; not used by the angle module).
; ($0A5C/$0A5D FREE 2026-08-09: zp_clip_cx/_hi died with EV16 — the
; crossing writes the projection input slots directly.)
; Working-saver for projecting X after project_y trashes vxlo/hi
; Per-seg back-face / linedef state

; ============================================================================
; Dynamic always-descend (2026-09-04) needs NO state of its own: the bits
; ARE NODE_DSGN b3 (LEFT far) / b2 (RIGHT far), the static always-descend
; policy's old home — retired because it measures a net loss once this
; runs.  There is no view-change wipe either (Eben, same day): after a
; teleport the stale bits cost ONE frame of over-descent and the judge
; clears them, so the engine carries no discontinuity guard.  The test
; benches step between unrelated poses in one engine, which is not a
; motion the predictor is meant to survive, so THEY clear the bits —
; bsp_render_6502.render_frame does it whenever the pose jumps.
; ============================================================================
